// Copyright Â© 2021-2022  Fanael Linithien
// SPDX-License-Identifier: AGPL-3.0-or-later
import com.github.spotbugs.snom.Confidence
import com.github.spotbugs.snom.Effort

plugins {
    id "application"
    id "com.github.spotbugs" version "5.0.9"
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly group: "com.github.spotbugs", name: "spotbugs-annotations", version: "4.7.1"
    compileOnly project(":auki")
    compileOnly project(":minijsr305")
    annotationProcessor project(":auki")
    testCompileOnly group: "com.github.spotbugs", name: "spotbugs-annotations", version: "4.7.1"
    testAnnotationProcessor project(":auki")
    testImplementation group: "org.junit.jupiter", name: "junit-jupiter-api", version: "5.8.2"
    testImplementation group: "org.assertj", name: "assertj-core", version: "3.23.1"
    testRuntimeOnly group: "org.junit.jupiter", name: "junit-jupiter-engine", version: "5.8.2"
}

spotbugs {
    toolVersion.set "4.7.1"
    showProgress.set true
    includeFilter.set file("spotbugs-include.xml")
    excludeFilter.set file("spotbugs-exclude.xml")
    effort.set Effort.MAX
    reportLevel.set Confidence.LOW
}

application {
    mainModule.set "greenspun"
    mainClass.set "greenspun.cli.Main"
}

java.toolchain {
    languageVersion = JavaLanguageVersion.of(18)
}

jar.manifest.attributes "Main-Class": application.mainClass.get()

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += [
            "--enable-preview",
            "-Xlint",
            "-Xlint:-preview,-processing,-requires-automatic,-serial",
    ]
}

tasks.withType(Test).configureEach {
    jvmArgs += "--enable-preview"
}

tasks.withType(JavaExec).configureEach {
    jvmArgs += "--enable-preview"
}

javadoc {
    options.addBooleanOption("-enable-preview", true)
    options.addStringOption("-release", java.toolchain.languageVersion.get().toString())
    options.addStringOption("-show-module-contents", "all")
}

test {
    useJUnitPlatform()
}

spotbugsMain.reports.configure {
    xml.enabled = false
    html.enabled = true
}
